
-- equation 4 : `x = x ◇ y` "left absorption"
-- equation 3744 : `x ◇ y = (x ◇ z) ◇ (w ◇ y)` "bypass"
example (G : Type) [Magma G] (ass : Equation4 G) : Equation3744 G :=
fun x y z w =>

  calc x ◇ y = x         := (ass x y).symm
  _ = x ◇ z              := ass x z
  _ = (x ◇ z) ◇ (w ◇ y)  := ass (x ◇ z) (w ◇ y)





-- equation 877 : `x = y ◇ ((x ◇ x) ◇ (z ◇ z))`
-- equation 2 :   `x = y`
example (G : Type) [Magma G] (ass : Equation877 G) : Equation2 G :=
fun x₁ y₁ =>

  let x₂ := x₁ ◇ x₁
  let x₄ := x₂ ◇ x₂
  let x₈ := x₄ ◇ x₄
  let y₂ := y₁ ◇ y₁
  let y₄ := y₂ ◇ y₂
  let y₈ := y₄ ◇ y₄

  have hy₈ : y₁ = y₈ := ass y₁ y₄ y₁

  calc x₁
  _ = y₁ ◇ x₄                := by rw [ass x₁ y₁ x₁]
  _ = y₁ ◇ (y₁ ◇ (x₈ ◇ x₈))  := by rw [ass x₄ y₁ x₄]
  _ = y₁ ◇ (y₁ ◇ (x₁ ◇ x₁))  := by rw [ass x₁ x₄ x₁]
  _ = y₁ ◇ (y₈ ◇ (x₁ ◇ x₁))  := by rw [hy₈]
  _ = y₄                     := by rw [←ass y₄ y₁ x₁]
  _ = y₁ ◇ (y₈ ◇ (y₁ ◇ y₁))  := by rw [ass y₄ y₁ y₁]
  _ = y₁ ◇ (y₁ ◇ (y₁ ◇ y₁))  := by rw [hy₈]
  _ = y₁ ◇ (y₁ ◇ (y₈ ◇ y₈))  := by rw [hy₈]
  _ = y₁ ◇ y₄                := by rw [ass y₄ y₁ y₄]
  _ = y₁                     := by rw [←ass y₁ y₁ y₁]


